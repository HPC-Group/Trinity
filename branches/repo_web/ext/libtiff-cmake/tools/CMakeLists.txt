# Build the TIFF tools

include_directories("${TIFF_CONFIG_DIR}" "../libtiff")
if (NEED_LIBPORT)
  include_directories("../port")
  add_definitions(-DNEED_LIBPORT)
endif()

if (MSVC)
  add_definitions(-D_CRT_SECURE_NO_WARNINGS)
endif()

# Helper macro to add an executable and link it to libtiff and libport
macro (add_tool exename)
  add_executable(${exename} ${ARGN})
  # Link against the static library since some tools use internal functions
  target_link_libraries(${exename} tiff_static port)
  set_target_properties(${exename} PROPERTIES FOLDER "tiff-tools")
  if (MSVC)
    # Disable POSIX-deprecation and downcasting warnings
    set_target_properties(${exename} PROPERTIES COMPILE_FLAGS "/wd4996 /wd4244")
  endif()
endmacro()

add_tool(bmp2tiff   bmp2tiff.c)
add_tool(fax2ps     fax2ps.c)
add_tool(fax2tiff   fax2tiff.c)
add_tool(gif2tiff   gif2tiff.c)
add_tool(pal2rgb    pal2rgb.c)
add_tool(ppm2tiff   ppm2tiff.c)
add_tool(ras2tiff   ras2tiff.c rasterfile.h)
add_tool(raw2tiff   raw2tiff.c)
add_tool(rgb2ycbcr  rgb2ycbcr.c)
add_tool(thumbnail  thumbnail.c)
add_tool(tiff2bw    tiff2bw.c)
add_tool(tiff2pdf   tiff2pdf.c)
add_tool(tiff2ps    tiff2ps.c)
add_tool(tiff2rgba  tiff2rgba.c)
add_tool(tiffcmp    tiffcmp.c)
add_tool(tiffcp     tiffcp.c)
add_tool(tiffcrop   tiffcrop.c)
add_tool(tiffdither tiffdither.c)
add_tool(tiffdump   tiffdump.c)
add_tool(tiffinfo   tiffinfo.c)
add_tool(tiffmedian tiffmedian.c)
add_tool(tiffset    tiffset.c)
add_tool(tiffsplit  tiffsplit.c)

if (HAVE_OPENGL)
  if (APPLE)
    add_definitions(-DHAVE_APPLE_OPENGL_FRAMEWORK)
  endif()
  if (MSVC)
    add_definitions(-DFREEGLUT_LIB_PRAGMAS=0)
  endif()
  include_directories(${GLUT_INCLUDE_DIR} ${OPENGL_INCLUDE_DIR})
  add_tool(tiffgt tiffgt.c)
  target_link_libraries(tiffgt ${GLUT_LIBRARIES} ${OPENGL_LIBRARIES})
endif()
